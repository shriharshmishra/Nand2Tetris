/** 
* Block class - denotes a rectangular block. 
*/
class Block {

    field int x1, y1, x2, y2;
    
    /** Create a new block with height, width and offset from left of the screen. */
    constructor Block new(int Aheight, int Awidth, int Aoffset) { 

        let x1 = (Aoffset * Awidth) + 1;
        let x2 = x1 + Awidth;
        let y2 = 255;
        let y1 = y2 - (Aheight * 25);

        //do logCoords();
        do draw();
        return this;
    }

   /**
   * Logs block coordinates on the screen. 
   */ 
   method void logCoords() {
        do Output.printString("Block(");
        do Output.printInt(x1);
        do Output.printString(", ");
        do Output.printInt(y1);
        do Output.printString(", ");
        do Output.printInt(x2);
        do Output.printString(", ");
        do Output.printInt(y2);
        do Output.printString(")");
        do Output.println();
        return;
    }

   /** Draws the block on the screen. */
   method void draw() {
      do Screen.setColor(true);
      do Screen.drawRectangle(x1, y1, x2, y2);
      return;
   }

   /** Method checks if the point is inside the Block */
   method boolean isPointInsideBlock(int x, int y) {
        if ((x > x1) & (x < x2) & (y > y1) & (y < y2)) { 
            return true;
        } else {
            return false;
        }
   }

   /** Disposes this block. */
   method void dispose() {
      do Memory.deAlloc(this);
      return;
   }
}
