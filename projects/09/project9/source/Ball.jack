/** Implements a graphical circle. */
class Ball {

   field int x, y; // screen location of the ball's centre in 2-d 
   field int radius; // radius of the Ball (circle in 2-d), in pixels

   /** Constructs a new circle with a given location and radius. */
   constructor Ball new(int Ax, int Ay, int Aradius) {
      let x = Ax;
      let y = Ay;
      let radius = Aradius;
      do draw();
      return this;
   }
   
   /** X-coordinate of the centre of the circle. */
   method int getX() {
      return x;
   }

   /** Y-coordinate of the centre of the circle. */
   method int getY() {
      return y;
   }

   /** Radius of the centre of the circle. */
   method int getRadius() {
     return radius;
   }

   /** Disposes this circle. */
   method void dispose() {
      do Memory.deAlloc(this);
      return;
   }

   /** Draws the circle on the screen. */
   method void draw() {
      do Screen.setColor(true);
      do Screen.drawCircle(x, y, radius);
      return;
   }

   /** Erases the circle from the screen. */
   method void erase() {
      do Screen.setColor(false);
      do Screen.drawCircle(x, y, radius);
      return;
   }


   /** Moves the circle up by 2 pixels. */
   method void moveUp() {
      if ((y - 1) > radius) {
         do Screen.setColor(false);
         do Screen.drawCircle(x, y, radius);
         let y = y - 1;
         do Screen.setColor(true);
         do Screen.drawCircle(x, y, radius);
      }
      return;
   }

   /** Moves the circle down by 2 pixels. */
   method void moveDown() {
      if ((y + radius) < 254) {
         do Screen.setColor(false);
         do Screen.drawCircle(x, y, radius);
         let y = y + 1;
         do Screen.setColor(true);
         do Screen.drawCircle(x, y, radius);
      }
      return;
   }

   /** Moves the circle left by 2 pixels. */
   method void moveLeft() {
      if ((x - 1) > radius) {
         do Screen.setColor(false);
         do Screen.drawCircle(x, y, radius);
         let x = x - 1;
         do Screen.setColor(true);
         do Screen.drawCircle(x, y, radius);
      }
      return;
   }

   /** Moves the circle right by 2 pixels. */
   method void moveRight() {
      if ((x + radius) < 511) {
         do Screen.setColor(false);
         do Screen.drawCircle(x, y, radius);
         let x = x + 1;
         do Screen.setColor(true);
         do Screen.drawCircle(x, y, radius);
      }
      return;
   }
}
